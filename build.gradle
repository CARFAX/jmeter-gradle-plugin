apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.kulya'
archivesBaseName = 'jmeter-gradle-plugin'
/**
 * Currently support JMeter version 2.6, 2.7, 2.8, 2.9
 */
ext.jmeterVersion = "2.7"
version = "1.3.1-$jmeterVersion"



sourceCompatibility=1.6
targetCompatibility=1.6

repositories {
    mavenCentral()
}

dependencies {
	compile gradleApi(),
        "org.apache.ant:ant:1.8.4",
        "org.apache.jmeter:ApacheJMeter_http:${jmeterVersion}",
		"org.apache.jmeter:ApacheJMeter_junit:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_java:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_report:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_jdbc:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_tcp:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_mail:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_ldap:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_ftp:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_functions:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_monitors:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter_jms:${jmeterVersion}",
        "org.apache.jmeter:ApacheJMeter:${jmeterVersion}",
        "org.beanshell:bsh-core:2.0b4"
    if (jmeterVersion > '2.7') {
        compile "org.apache.jmeter:ApacheJMeter_native:${jmeterVersion}"
    }
	
	groovy localGroovy()
	
	testCompile "junit:junit:4.11"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

processResources.doLast {
    ant.replace(
            file: "${project.buildDir}/resources/main/jmeter-plugin.properties",
            token: "@jmeter.version@",
            value: "$jmeterVersion"
    )
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }


            pom.project {
                name 'jmeter-gradle-plugin'
                packaging 'jar'
                description 'Add support for running jmeter tests from gradle'
                url 'https://github.com/kulya/jmeter-gradle-plugin'
                inceptionYear '2011'

                scm {
                    url 'scm:git@github.com:kulya/jmeter-gradle-plugin.git'
                    connection 'scm:git@github.com:kulya/jmeter-gradle-plugin.git'
                    developerConnection 'scm:git@github.com:kulya/jmeter-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'kulya'
                        name 'Alexandr Kulik'
                        url 'https://github.com/kulya/'
                        email 'alexandr.kulik1988@gmail.com'
                        roles { role 'Developer' }
                    }
                }

            }
        }
    }
}

