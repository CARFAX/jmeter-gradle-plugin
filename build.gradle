apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'org.veil.gradle.plugins'
archivesBaseName = 'jmeter-gradle-plugin'
/**
 * Currently support JMeter version 2.5.3, 2.6, 2.7
 */
ext.jmeterVersion = "2.7"
version = "1.3-$jmeterVersion-SNAPSHOT"



sourceCompatibility=1.6
targetCompatibility=1.6

repositories {
    maven {
        url "http://repo.kulya.info/content/groups/public/"
    }
}

dependencies {
	compile gradleApi(),
        "org.apache.jmeter:jmeter-http:${jmeterVersion}",
		"org.apache.jmeter:jmeter-junit:${jmeterVersion}",
        "org.apache.jmeter:jmeter-java:${jmeterVersion}",
        "org.apache.jmeter:jmeter-report:${jmeterVersion}",
        "org.apache.jmeter:jmeter-jdbc:${jmeterVersion}",
        "org.apache.jmeter:jmeter-tcp:${jmeterVersion}",
        "org.apache.jmeter:jmeter-mail:${jmeterVersion}",
        "org.apache.jmeter:jmeter-ldap:${jmeterVersion}",
        "org.apache.jmeter:jmeter-ftp:${jmeterVersion}",
        "org.apache.jmeter:jmeter-functions:${jmeterVersion}",
        "org.apache.jmeter:jmeter-monitor-components:${jmeterVersion}",
        "org.apache.jmeter:jmeter-jms:${jmeterVersion}",
        "org.beanshell:bsh-core:2.0b4"

	
	groovy localGroovy()
	
	testCompile "junit:junit:4.10"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

artifacts {
    archives sourceJar
}

processResources.doLast {
    ant.replace(
            file: "${project.buildDir}/resources/main/jmeter-plugin.properties",
            token: "@jmeter.version@",
            value: "$jmeterVersion"
    )
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://repo.kulya.info/content/repositories/releases/") {
                authentication(userName: kulyaMvnDeployerUser, password: kulyaMvnDeployerPass)
            }

            snapshotRepository(url: "http://repo.kulya.info/content/repositories/snapshots/") {
                authentication(userName: kulyaMvnDeployerUser, password: kulyaMvnDeployerPass)
            }
        }
    }
}

